response = _{
    SOI ~ status_line ~ headers ~ NEWLINE* ~ (chunked_body | content_length_body) ~ NEWLINE* ~ EOI
}

status_line = { "HTTP/" ~ ASCII_DIGIT+ ~ "." ~ ASCII_DIGIT+ ~ " " ~ ASCII_DIGIT+ ~ " " ~ (!NEWLINE ~ ANY)+ ~ NEWLINE }

headers      = _{ header* }
header       =  { header_name ~ ": " ~ header_value ~ NEWLINE }
header_name  =  { (ASCII_ALPHANUMERIC | "-")+ }
header_value =  { (ASCII_ALPHANUMERIC | "-" | ";" | " " | ":" | "," | "/" | "'" | "\"" | "." | "=" | "+" | "*" | "_" | "{" | "}")+ }

// Chunked transfer encoding: hex_size \n body \n 0 \n
chunked_body = {
    ASCII_HEX_DIGIT+ ~ NEWLINE* ~ json ~ NEWLINE* ~ ASCII_HEX_DIGIT+ ~ NEWLINE*
}

// Content-length encoding: just the body directly
content_length_body = { json }

ws = _{ " " | "\t" }

object  =  {
    "{" ~ ws* ~ "}"
  | "{" ~ ws* ~ pair ~ (ws* ~ "," ~ ws* ~ pair)* ~ ws* ~ "}"
}
pair    =  { string ~ ws* ~ ":" ~ ws* ~ value }
array   =  {
    "[" ~ ws* ~ "]"
  | "[" ~ ws* ~ value ~ (ws* ~ "," ~ ws* ~ value)* ~ ws* ~ "]"
}
value   = _{ object | array | string | number | boolean | null }
boolean =  { "true" | "false" }
null    =  { "null" }
string  = ${ "\"" ~ inner ~ "\"" }
inner   = @{ char* }
char    =  {
    !("\"" | "\\") ~ ANY
  | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
  | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}
number  = @{
    "-"? ~ ("0" | ASCII_NONZERO_DIGIT ~ ASCII_DIGIT*) ~ ("." ~ ASCII_DIGIT*)? ~ (^"e" ~ ("+" | "-")? ~ ASCII_DIGIT+)?
}
json    =  { object | array }
